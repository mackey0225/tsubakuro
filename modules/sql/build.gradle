plugins {
    id 'tsubakuro.jni-library-conventions'

    id 'com.google.protobuf' version '0.8.16'
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.23.0'
    implementation 'io.grpc:grpc-protobuf:1.23.0'
    implementation 'io.grpc:grpc-stub:1.23.0'
    implementation 'com.google.protobuf:protobuf-java:3.0.0'
}

sourceSets {
    main {
        proto {
            srcDir("src/main/protos")
        }
    }
}

spotbugsMain.enabled = false
checkstyleMain.enabled = false

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = "com.google.protobuf:protoc:3.8.0"
    }
}


compileTestJava {
    options.headerOutputDirectory = file("$project.projectDir/src/test/native/include")
}

task cmakeTestConfigure {
    doLast {
        exec {
            mkdir "$project.projectDir/src/test/native/build"
            workingDir "$project.projectDir/src/test/native/build"
            commandLine "cmake", ".."
        }
    }
}

task cmakeTestBuild {
    doLast {
        exec {
            mkdir "$project.projectDir/src/test/native/build"
            workingDir "$project.projectDir/src/test/native/build"
            commandLine "cmake", "--build", ".", "--", "-j"
        }
    }
}

test.dependsOn cmakeTestBuild
cmakeTestBuild.dependsOn cmakeTestConfigure
cmakeTestConfigure.dependsOn compileTestJava

tasks.named('test') {
    systemProperty 'java.library.path', "$project.projectDir/src/main/native/build/src:$project.projectDir/src/test/native/build/src"
}
