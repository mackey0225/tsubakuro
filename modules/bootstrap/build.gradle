plugins {
    id 'tsubakuro.java-library-conventions'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

dependencies {
    // dependent projects
    implementation project(':tsubakuro-console')
    implementation project(':tsubakuro-session')
    implementation project(':tsubakuro-common')
    implementation project(':tsubakuro-ipc')
    implementation project(':tsubakuro-stream')

    // peripherals
    implementation 'org.slf4j:slf4j-simple:1.7.36'

    testImplementation 'org.slf4j:slf4j-simple:1.7.36'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

application {
    applicationName = 'Tsubakuro'
    mainClass = 'com.tsurugidb.tsubakuro.bootstrap.Main'
}

run {
    description 'run SQL script : -Dscript=</path/to/script> -Dconnection=<endpoint-uri> [-DlogLevel=<level>]'
    args = [ System.getProperty('script', '(unspecified)'), System.getProperty('connection', 'unspecified:unspecified') ]
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', System.getProperty('logLevel', 'INFO')
    systemProperty 'java.library.path', "$project.projectDir/../ipc/src/main/native/build/src"
    jvmArgs = ['--add-opens=java.base/java.nio=ALL-UNNAMED']
}

shadowJar {
    archiveBaseName = 'tsubakuro-bootstrap'
    archiveClassifier = 'all'
    mergeServiceFiles()
    manifest.attributes (
        'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
        'Build-Revision' : System.getenv("GITHUB_SHA") ?: "",
        'Created-By'     : "Gradle ${gradle.gradleVersion}",
        'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    )
}
